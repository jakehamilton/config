{ options, config, pkgs, lib, ... }:

with lib;
with lib.plusultra;
let cfg = config.plusultra.nix;
in
{
  options.plusultra.nix = with types; {
    enable = mkBoolOpt true "Whether or not to manage nix configuration.";
    package = mkOpt package pkgs.nixUnstable "Which nix package to use.";
  };

  config = mkIf cfg.enable {
    environment.systemPackages = with pkgs; [
      deploy-rs
      nixfmt
      nix-index
      nix-prefetch-git
    ];

    nix =
      let users = [ "root" config.plusultra.user.name ];
      in
      {
        package = cfg.package;

        settings = {
          experimental-features = "nix-command flakes";
          http-connections = 50;
          warn-dirty = false;
          log-lines = 50;
          sandbox = "relaxed";
          auto-optimise-store = true;
          trusted-users = users;
          allowed-users = users;

          # @NOTE(jakehamilton): This configuration is generated by nix-installer so I'm adding it here in
          # case it becomes important.
          extra-nix-path = "nixpkgs=flake:nixpkgs";
          build-users-group = "nixbld";
        };
        #// (lib.optionalAttrs config.plusultra.tools.direnv.enable {
        #  keep-outputs = true;
        #  keep-derivations = true;
        #});

        gc = {
          automatic = true;
          interval = { Day = 7; };
          options = "--delete-older-than 30d";
          user = config.plusultra.user.name;
        };

        # flake-utils-plus
        generateRegistryFromInputs = true;
        generateNixPathFromInputs = true;
        linkInputs = true;
      };
  };
}
